// Copyright (c) 2018  Brendan Molloy <brendan@bbqsrc.net>
//
// Licensed under the Apache License, Version 2.0 <LICENSE-APACHE or
// http://www.apache.org/licenses/LICENSE-2.0> or the MIT license
// <LICENSE-MIT or http://opensource.org/licenses/MIT>, at your
// option. This file may not be copied, modified, or distributed
// except according to those terms.

nl = _{ "\n" | "\r\n" }
space = _{ " " | "\t" }
comment = _{ "#" ~ (!nl ~ any)* ~ nl }
whitespace = _{ " " }
tag_prefix = _{ "@" }
docstring_quote = _{ "\"\"\"" }

tag = {
    tag_prefix
    ~ (
        !(space | nl | tag_prefix)
        ~ any
    )+
}

tags = {
    (
        !nl
        ~ tag
    )+
}

docstring = {
    docstring_quote
    ~ docstring_value
    ~ space*
    ~ docstring_quote
}

docstring_value = @{
    nl ~
    " "* ~
    (
        !(
            (space)*
            ~ docstring_quote
        )
        ~ any
    )*
}

table_field = {
    (
        !("|")
        ~ any
    )+
}

table_header = {
    nl?
    ~ space*
    ~ "|"
    ~ (
        !nl ~
        table_field
        ~ "|"
    )+
}

table_row = {
    nl?
    ~ space*
    ~ "|"
    ~ (
        !nl ~
        table_field
        ~ "|"
    )+
}

datatable = {
    table_header
    ~ (
        table_row
    )+
}

step_kw = {
    ("Given" | "When" | "Then" | "And" | "But")
}

step_body = {
    (
        !(nl)
        ~ any
    )+
}

step = {
    step_kw
    ~ space*
    ~ step_body
    ~ (nl | eoi)
    ~ space*
    ~ (
        (datatable | docstring)
        ~ space*
        ~ (nl | eoi)
    )?
}

scenario_kw = _{
    ("Scenario Outline" | "Scenario" | "Example")
    ~ ":" ~ space*
}

examples_kw = _{ "Examples:" }

examples = {
    (space | nl)*
    ~ (tags ~ nl)?
    ~ examples_kw
    ~ datatable
    ~ nl
}

scenario_name = {
    (
        !(nl)
        ~ any
    )+
}

scenario_steps = {
    (step)+
}

scenario = {
    (space | nl)*
    ~ (tags ~ nl)?
    ~ scenario_kw ~ scenario_name ~ nl
    ~ scenario_steps
    ~ (nl+ ~ examples)?
}

background_kw = _{
    "Background:" ~ space*
}

background = {
    (space | nl)*
    ~ background_kw ~ nl
    ~ (step)+ ~ nl
}

feature_kw = {
    "Feature:" ~ space*
}

feature_body = {
    (
        !(nl)
        ~ any
    )+
}

feature_description_line = _{
    (space | nl)* ~
    !(tag_prefix | background_kw | scenario_kw) ~
    (
        !(nl) ~
        any
    )+
}

feature_description = {
    (space | nl)* ~
    !(tag_prefix | background_kw | scenario_kw) ~
    (
        !(tag_prefix | background_kw | scenario_kw) ~
        feature_description_line
    )+
}

feature = {
    (space | nl)*
    ~ (tags ~ nl)?
    ~ feature_kw
    ~ feature_body
    ~ feature_description?
    ~ background?
    ~ scenario*
}

main = {
    soi
    ~ (space | nl)*
    ~ feature
    ~ (space | nl)*
    ~ eoi
}
